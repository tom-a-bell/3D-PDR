!=======================================================================
!
!  Calculate the level populations at LTE for the given species.
!
!-----------------------------------------------------------------------
SUBROUTINE CALCULATE_LTE_POPULATIONS(NLEVEL,POPULATION,ENERGY,WEIGHT,DENSITY,TEMPERATURE)

   USE HEALPIX_TYPES
   IMPLICIT NONE

   INTEGER(KIND=I4B), INTENT(IN)  :: NLEVEL
   REAL(KIND=DP),     INTENT(IN)  :: ENERGY(1:NLEVEL),WEIGHT(1:NLEVEL)
   REAL(KIND=DP),     INTENT(IN)  :: DENSITY,TEMPERATURE
   REAL(KIND=DP),     INTENT(OUT) :: POPULATION(1:NLEVEL)

   INTEGER(KIND=I4B) :: ILEVEL
   REAL(KIND=DP) :: PARTITION_FUNCTION

   PARTITION_FUNCTION=0.0D0
   DO ILEVEL=1,NLEVEL
      POPULATION(ILEVEL)=WEIGHT(ILEVEL)*EXP(-ENERGY(ILEVEL)/(KB*TEMPERATURE))
      PARTITION_FUNCTION=PARTITION_FUNCTION+POPULATION(ILEVEL)
   END DO
   POPULATION=POPULATION*DENSITY/PARTITION_FUNCTION

!  Check that the sum of the level populations matches the total density to within 0.1%
   IF(ABS(SUM(POPULATION)-DENSITY)/DENSITY.GT.1.0D-3) THEN
      WRITE(6,*) 'ERROR: Sum of LTE level populations differs from the total density by ', &
		& NINT(1.0D2*ABS(SUM(POPULATION)-DENSITY)/DENSITY),'%'

   END IF

   RETURN
END SUBROUTINE CALCULATE_LTE_POPULATIONS
!=======================================================================
